# configuration
set output "./tmp.pdf"
set datafile separator ","
set terminal png size 1080,1080
set terminal pdfcairo font "sans,12" size 6,5
set title "\n"
set logscale x 10
set logscale y 10
set logscale y2 10
set yrange [1.0E-9:1.0E-5]
set y2range [1.0E-2:1.0E2]
set y2tics
set xrange [1.0E-37:1.0E-33]
set cbrange [0:1]
set arrow from 1.0E-37,2.176434E-8 to 1.0E-33,2.176434E-8 nohead # mP
set ytics add ("m_P" 2.176434E-8)
set arrow from 1.616225E-35,1.0E-9 to 1.616225E-35,1.0E-05 nohead # lP
set xtics add ("l_P" 1.616225E-35)
set ylabel 'abs(M_{total}) (kg)'
set y2label 'ratio'
set xlabel 'radius (m)'
set tmargin 3
set bmargin 4
set rmargin 10

# string to color pallete from https://stackoverflow.com/questions/9082807/gnuplot-plot-points-with-color-based-values-in-one-string-column-and-show-strin
set palette maxcolors 2
set palette defined (0 "blue", 1 "red")
unset colorbox
map_color(string) = (\
string eq 'pos_mass' ? 0 :\
string eq 'neg_mass' ? 1 :\
2)

# generate plot
plot "tmp.csv" using 1:2:(map_color(stringcolumn(10))) with points pt 7 ps 0.05 lc palette notitle axis x1y1,\
     "tmp.csv" using 1:8 with points pt 7 ps 0.05 lc rgb "gray" notitle axis x1y2,\
     "tmp.csv" using 1:9 with points pt 7 ps 0.05 lc rgb "light-green" notitle axis x1y2,\
     NaN with points pt 7 ps 0.25 lc rgb "blue" title "positive mass (fwd time) [LH]",\
     NaN with points pt 7 ps 0.25 lc rgb "red" title "\"negative\" mass (rev time) [LH]",\
     NaN with points pt 7 ps 0.25 lc rgb "gray" title "M_{em} / M_{total} [RH]",\
     NaN with points pt 7 ps 0.25 lc rgb "light-green" title "J_{eff} / (‚Ñè/2) [RH]"
